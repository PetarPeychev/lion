# The main reason we import core by default...
(import)
(".lion" + read parse eval)
def

# List operations
(append) 
(() cons cat) 
def

(prepend) 
(swap cons) 
def

# Other random stuff
(println)
(print "\n" print)
def

(empty?)
(() eq)
def

(map) # [a b c] [f] -> [fa fb fc]
(
    (dup empty?) dip swap
    (drop)
    (
        # [a b c] [f]
        (() swap uncons (append) dip) dip # [a] [b c] [f]
        swap # [a] [f] [b c]
        (dup (_apply) dip) dip # [fa] [f] [b c]
        swap # [fa] [b c] [f]
        _map
    )
    ifte
)
def

(_apply) # [a b c] [f] -> [fa b c]
((uncons swap) dip eval swap cons)
def

(_map) # [a] [b c] [f] -> [a fb fc]
(
    (dup empty?) dip swap
    (drop drop)
    (
        # [fa] [b c] [f]
        (() swap uncons (append) dip) dip # [fa] [b] [c] [f]
        swap # [fa] [b] [f] [c]
        (dup (_apply cat) dip) dip # [fa] [fb] [f] [c]
        swap # [fa fb] [c] [f]
        _map
    )
    ifte
)
def

(printall) 
(
    dup empty? (drop) 
    (uncons swap string println printall) 
    ifte
) 
def